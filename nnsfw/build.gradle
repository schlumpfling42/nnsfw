plugins {
  id 'com.github.johnrengelman.shadow' version '5.2.0'
  id 'java'
  id 'application'
  id 'maven-publish'
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

version = '0.0.13'

def undertowVersion = '2.0.1.Final'

repositories {
    mavenCentral()
}

mainClassName = "net.nnsfw.ApplicationServer"

dependencies {
    compile "io.undertow:undertow-core:$undertowVersion",
            "io.undertow:undertow-servlet:$undertowVersion",
            "com.fasterxml.jackson.core:jackson-databind:2.9.6",
            "javax.inject:javax.inject:1",
            "com.google.api-client:google-api-client:1.32.+",
            "com.google.apis:google-api-services-oauth2:v2-rev157-1.25.+",
            "javax.transaction:javax.transaction-api:1.3",
            "javax.persistence:javax.persistence-api:2.2",
            "org.flywaydb:flyway-core:6.1.1",
            "cglib:cglib-nodep:3.3.0",
            "org.yaml:snakeyaml:1.25"
}

shadowJar {
  configurations = [project.configurations.compile]
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/schlumpfling42/server-fw")
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("USERNAME")
                password = project.findProperty("gpr.key") ?: System.getenv("PASSWORD")
            }
        }
    }
    publications {
        gpr(MavenPublication) {
            from(components.java)
        }
    }
}
